///$tab Main
SET ThousandSep='.';
SET DecimalSep=',';
SET MoneyThousandSep='.';
SET MoneyDecimalSep=',';
SET MoneyFormat='€#.##0,00;-€#.##0,00';
SET TimeFormat='hh:mm:ss';
SET DateFormat='DD-MM-YYYY';
SET TimestampFormat='DD-MM-YYYY hh:mm:ss[.fff]';
SET FirstWeekDay=6;
SET BrokenWeeks=1;
SET ReferenceDay=0;
SET FirstMonthOfYear=1;
SET CollationLocale='en-US';
SET CreateSearchIndexOnReload=1;
SET MonthNames='Jan;Feb;Mar;Apr;May;Jun;Jul;Aug;Sep;Oct;Nov;Dec';
SET LongMonthNames='January;February;March;April;May;June;July;August;September;October;November;December';
SET DayNames='Mon;Tue;Wed;Thu;Fri;Sat;Sun';
SET LongDayNames='Monday;Tuesday;Wednesday;Thursday;Friday;Saturday;Sunday';
SET NumericalAbbreviation='3:k;6:M;9:G;12:T;15:P;18:E;21:Z;24:Y;-3:m;-6:μ;-9:n;-12:p;-15:f;-18:a;-21:z;-24:y';




let vMaxDate  = '=DATE(max("Order Date"),' & chr(39) & 'DD MMM YYYY' & chr(39) & ')';
let vMaxDay   = '=day(max("Order Date"))';
let vMaxMonth  = '=(month(max("Order Date")))';
let vMaxYear  = '=max(Year)';
let vPriorMonth  = '=month(addmonths(max("Order Date"),-1))';
let vPriorMonthYear = '=Year(addmonths(max("Order Date"),-1))';
let vPriorYear  = '=vMaxYear-1';
let vPriorYearDate = '=date(addyears(max("Order Date"),-1),' & chr(39) & 'DD MMM YYYY' & chr(39) & ')';




orders:
LOAD
    "Row ID",
    "Order ID",
    DATE("Order Date" + 859) AS "Order Date",
    "Ship Date",
    "Ship Mode",
    "Customer ID",
    "Customer Name",
    Segment,
    Country,
    City,
    State,
    "Postal Code",
    Region,
    "Product ID",
    Category,
    "Sub-Category",
    "Product Name",
    NUM(Sales, '$(MoneyFormat)') AS Sales,
    Quantity,
    Discount,
    Profit,
    Sales-Profit AS COGS
FROM [lib://Set_Analysis/Sample - Superstore.xls]
(biff, embedded labels, table is Orders$)
Where "Row ID" < 2000 AND Segment = 'Consumer';


minmax_temp:
LOAD 
 MIN("Order Date") AS MinDate,
 MAX("Order Date") AS MaxDate
RESIDENT orders;

LET vmindate = NUM(PEEK('MinDate', 0, 'minmax_temp'));
LET vmaxdate = NUM(PEEK('MaxDate', 0, 'minmax_temp'));
LET vToday = NUM('07-05-2020');

DROP TABLE minmax_temp;

TempCalendar:
LOAD DATE($(vmindate) + ROWNO() -1) AS Date
AUTOGENERATE $(vmaxdate) - $(vmindate) +1;


//**** Create the Master Calendar ****
MasterCalendar:
LOAD	
Date AS "Order Date",
Week(Date) as Week,
Year(Date) as Year,
Month(Date) as Month,
Day(Date) as Day,
WeekDay(Date) as WeekDay,
'Q' & Ceil(Month(Date) / 3) as Quarter, 
Date(MonthStart(Date),'MM-YYYY') as MonthYear,
Week(Date) &'-'& If(Week(Date)=1 AND Month(Date)=12,Year(Date)+1, If(Match(Week(Date), 52, 53) AND 
Month(Date)=1, Year(Date)-1, Year(Date))) as WeekYear,
InYearToDate(Date, $(vToday), 0) * -1 as CYTDFlag,
InYearToDate(Date, $(vToday), -1) * -1 as LYTDFlag,
INYEAR(Date, $(vToday), 0) * -1 as CYFlag,
INYEAR(Date, $(vToday), -1) * -1 as LYFlag,
INMONTH(Date, $(vToday), 0) * -1 as CMFlag,
INMONTH(Date, $(vToday), -1) * -1 as LMFlag,
INMONTH(Date, $(vToday), -12) * -1 as CMLYFlag,
InMonthToDate(Date, $(vToday), 0) * -1 as CMTDFlag,
InMonthToDate(Date, $(vToday), -1) * -1 as LMTDFlag,
InMonthToDate(Date, $(vToday), -12) * -1 as CMLYTDFlag,
If(Date > ($(vToday)-28) and Date <= $(vToday),1, 0) as R28, // Rolling 28 Days
If(Date > ($(vToday)-56) and Date <= ($(vToday)-28),1, 0) as PreR28, // Rolling 29 -56
If(Date> addmonths($(vToday),-12) and Date <= $(vToday) ,1,0) as R12M // Rolling 12 months


RESIDENT TempCalendar;



DROP TABLE TempCalendar;